project('POB Frontend', 'cpp', default_options : ['cpp_std=c++11'])

qt5_dep = dependency('qt5', modules : ['Gui'])
lua_dep = dependency('lua51')
#lua_dep = dependency('luajit')
# NB on OSX you also need to invoke meson like so, because luajit:
# LDFLAGS="-pagezero_size 10000 -image_base 100000000" meson pobfrontend build
if build_machine.system() == 'darwin'
   gl_dep = dependency('appleframeworks', modules: ['OpenGL'])
else
   gl_dep = dependency('gl')
endif
zlib_dep = dependency('zlib')

# Import the extension module that knows how
# to invoke Qt tools.
qt5 = import('qt5')
prep = qt5.preprocess(moc_headers : ['subscript.hpp', 'pobwindow.hpp'])

extra_args = []
extra_ld_args = []
if meson.get_compiler('cpp').get_id() == 'gcc'
  extra_args = ['-Wno-reorder']
  sanitizer = []
  #sanitizer = ['-fsanitize=leak']
  extra_args += sanitizer
  extra_ld_args = sanitizer
endif

executable('pobfrontend',
  sources : ['main.cpp', prep],
  dependencies : [qt5_dep, gl_dep, zlib_dep, lua_dep],
  cpp_args: extra_args,
  link_args: extra_ld_args
  )
